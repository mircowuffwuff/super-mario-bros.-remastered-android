extends Control

var selected_index := 0

signal selected
signal cancelled
var active := false

var player_id := 0

var character_sprite_jsons := [
	"res://Assets/Sprites/Players/Mario/Small.json",
	"res://Assets/Sprites/Players/Luigi/Small.json",
	"res://Assets/Sprites/Players/Toad/Small.json",
	"res://Assets/Sprites/Players/Toadette/Small.json"
]

const PLAYER_SCENE = "res://Scenes/Prefabs/Entities/Player.tscn"


func _process(_delta: float) -> void:
	if active:
		handle_input()

func _ready() -> void:
	update_sprites()

func get_custom_characters() -> void:
	Player.CHARACTERS = ["Mario", "Luigi", "Toad", "Toadette"]
	Player.CHARACTER_NAMES = ["CHAR_MARIO", "CHAR_LUIGI", "CHAR_TOAD", "CHAR_TOADETTE"]
	AudioManager.character_sfx_map.clear()
	
	var idx := 0
	for i in Player.CHARACTERS:
		var path = ResourceSetter.get_pure_resource_path("res://Assets/Sprites/Players/" + i + "/CharacterInfo.json")
		if FileAccess.file_exists(path):
			var json = JSONParser.parse_to_dict(path)
			Player.CHARACTER_NAMES[idx] = json.name
		path = ResourceSetter.get_pure_resource_path("res://Assets/Sprites/Players/" + i + "/CharacterColour.json")
		if FileAccess.file_exists(path):
			Player.CHARACTER_COLOURS[idx] = (path)
		idx += 1
	
	var base_path = Global.config_path
	var char_dir = base_path.path_join("custom_characters") 
	for i in DirAccess.get_directories_at(char_dir):
		var char_path = char_dir.path_join(i)
		var char_info_path = char_path.path_join("CharacterInfo.json")
		if FileAccess.file_exists(char_info_path):
			var json = JSONParser.parse_to_dict(char_path.path_join("CharacterInfo.json"))
			if json == null:
				continue
			if json.has("physics"):
				if json.physics.has("PHYSICS_PARAMETERS") == false:
					json = CustomCharacterUpdater.update_json(json)
					Global.log_comment("Updated CharacterInfo for: " + i)
					JSONParser.save_to_file(json, char_path.path_join("CharacterInfo.json"))
			Player.CHARACTERS.append(i)
			
			var character_doesnt_have := []
			if (json.has("name")):
				Player.CHARACTER_NAMES.append(json.name)
			else:
				Player.CHARACTER_NAMES.append("???")
				character_doesnt_have.append("name")
			
			if FileAccess.file_exists(char_path.path_join("CharacterColour.json")):
				Player.CHARACTER_COLOURS.append(char_path.path_join("CharacterColour.json"))
			else:
				Player.CHARACTER_COLOURS.append(null)
				character_doesnt_have.append("colour")
			
			if FileAccess.file_exists(char_path.path_join("LifeIcon.json")):
				GameHUD.character_icons.append(char_path.path_join("LifeIcon.json"))
			else:
				GameHUD.character_icons.append(null)
				character_doesnt_have.append("icon")
				
			if FileAccess.file_exists(char_path.path_join("ColourPalette.json")):
				Player.CHARACTER_PALETTES.append(char_path.path_join("ColourPalette.json"))
			else:
				Player.CHARACTER_PALETTES.append(null)
				character_doesnt_have.append("palette")
			if (!FileAccess.file_exists(char_path.path_join("CheckpointFlag.json"))):
				character_doesnt_have.append("checkpoint flag")
			
			AudioManager.character_sfx_map[i] = JSONParser.parse_to_dict(char_path.path_join("SFX.json"))
			
			if (character_doesnt_have.size() != 0):
				var final_list_str := ""
				
				var cur_idx := 0
				for missing in character_doesnt_have:
					if cur_idx != 0:
						if (cur_idx == character_doesnt_have.size() - 1):
							final_list_str += " and "
						else:
							final_list_str += ", "
					
					final_list_str += missing
					cur_idx += 1

				# DawnLR: Yeah, kind of unnecessary, but come on, at least it's cool!
				Global.log_warning("Character: \"%s\" is missing: %s!" % [i, final_list_str])

func open() -> void:
	get_custom_characters()
	show()
	selected_index = int(Global.player_characters[player_id])
	update_sprites()
	await get_tree().physics_frame
	grab_focus()
	active = true

func handle_input() -> void:
	if Global.multibind_action_just_pressed("ui_left"):
		selected_index = wrap(selected_index - 1, 0, Player.CHARACTERS.size())
		update_sprites()
		if Settings.file.audio.extra_sfx == 1:
			AudioManager.play_global_sfx("menu_move")
	elif Global.multibind_action_just_pressed("ui_right"):
		selected_index = wrap(selected_index + 1, 0, Player.CHARACTERS.size())
		update_sprites()
		if Settings.file.audio.extra_sfx == 1:
			AudioManager.play_global_sfx("menu_move")
	if Global.multibind_action_just_pressed("ui_accept"):
		Global.player_characters[player_id] = (selected_index)
		var characters: Array = Global.player_characters
		for i in characters:
			if int(i) > 3:
				characters = [0, 0, 0, 0]
		Settings.file.game.characters = characters
		Settings.save_settings()
		selected.emit()
		close()
	elif Global.multibind_action_just_pressed("ui_back"):
		close()
		cancelled.emit()

func update_sprites() -> void:
	%Left.force_character = Player.CHARACTERS[wrap(selected_index - 1, 0, Player.CHARACTERS.size())]
	%Selected.force_character = Player.CHARACTERS[wrap(selected_index, 0, Player.CHARACTERS.size())]
	%Right.force_character = Player.CHARACTERS[wrap(selected_index + 1, 0, Player.CHARACTERS.size())]
	for i in [%Left, %Selected, %Right]:
		i.update()
		i.play("Pose" if i == %Selected else "FaceForward")
	print(Player.CHARACTER_COLOURS[selected_index])
	if (Player.CHARACTER_COLOURS[selected_index] != null):
		%PlayerColourTexture.json_path = Player.CHARACTER_COLOURS[selected_index]
	%CharacterName.text = tr(Player.CHARACTER_NAMES[selected_index])
	$Panel/MarginContainer/VBoxContainer/CharacterName/TextShadowColourChanger/ColourPaletteSampler.texture = %ColourPaletteSampler.texture
	$Panel/MarginContainer/VBoxContainer/CharacterName/TextShadowColourChanger.handle_shadow_colours()
func select() -> void:
	selected.emit()
	hide()
	active = false

func close() -> void:
	active = false
	hide()


func on_selected() -> void:
	pass # Replace with function body.
