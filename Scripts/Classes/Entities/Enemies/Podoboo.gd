class_name Podoboo
extends Node2D

var velocity := 5.0

var play_sfx := false

@onready var starting_y := global_position.y
@export_range(0, 3) var jump_delay := 1
var can_jump := true

signal killed

const BASE_LINE := 48

@onready var old_position = global_position

func _physics_process(delta: float) -> void:
	if $TrackJoint.is_attached == false:
		handle_movement(delta)
		$Sprite.flip_v = velocity > 0
	else:
		handle_rotation.call_deferred()

func handle_rotation() -> void:
	$Sprite.flip_v = false
	var direction = old_position - global_position
	$Sprite.rotation = (old_position - global_position).normalized().angle() - deg_to_rad(90)
	old_position = global_position

func handle_movement(delta: float) -> void:
	velocity += (5 / delta) * delta
	velocity = clamp(velocity, -INF, 280)
	global_position.y += velocity * delta
	global_position.y = clamp(global_position.y, -INF, BASE_LINE)
	if global_position.y >= BASE_LINE and can_jump:
		can_jump = false
		do_jump()

func do_jump() -> void:
	if jump_delay > 0:
		$Timer.start(jump_delay)
		await $Timer.timeout
	if play_sfx:
		AudioManager.play_sfx("podoboo", global_position)
	velocity = calculate_jump_height()
	await get_tree().physics_frame
	can_jump = true

func calculate_jump_height() -> float:
	global_position.y = BASE_LINE
	return -sqrt(2 * 5 * abs(starting_y - (global_position.y))) * 8
	

func damage_player(player: Player, type: String = "Normal") -> void:
	player.damage(type if type != "Normal" else "")

const SMOKE_PARTICLE = preload("uid://d08nv4qtfouv1")

func flag_die() -> void:
	if $VisibleOnScreenEnabler2D.is_on_screen():
		die()

func die() -> void:
	killed.emit()
	queue_free()

func die_from_hammer() -> void:
	AudioManager.play_sfx("hammer_hit", global_position)
	killed.emit()
	queue_free()
