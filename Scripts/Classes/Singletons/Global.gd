extends Node

var level_theme := "Overworld":
	set(value):
		level_theme = value
		update_theme()
	get:
		if theme_override:
			return theme_override
		return level_theme
var theme_time := "Day":
	set(value):
		theme_time = value
		update_theme()
	get:
		if time_override:
			return time_override
		return theme_time

var theme_override := ""
var time_override := ""
var music_override := ""
var primary_bg_override := -1
var secondary_bg_override := -1
var liquid_override := -1
var particle_override := -1
var extra_music_override := ""
var level_metadata := {}

signal level_theme_changed

const BASE64_CHARSET := "ABCDEFGHIJKLMNOPQRSTUVWXYZabcdefghijklmnopqrstuvwxyz0123456789+/"

const VERSION_CHECK_URL := "https://cdn.jsdelivr.net/gh/JHDev2006/Super-Mario-Bros.-Remastered-Public@main/version.txt"
const SNAPSHOT_CHECK_URL := "https://cdn.jsdelivr.net/gh/JHDev2006/Super-Mario-Bros.-Remastered-Public@refs/heads/1.1/snapshot_version.txt"
@onready var screen_shaker: Node = $ScreenShaker

var entity_gravity := 10.0
var entity_max_fall_speed := 280

var level_editor: LevelEditor = null
var current_level: Level = null

var second_quest := false
var extra_worlds_win := false
const lang_codes := ["en", "fr", "es", "de", "it", "pt_br", "pl", "tr", "ru", "jp", "fil", "id", "gal"]

var config_path := "user://"

var rom_path := ""
var rom_assets_exist := false
var ROM_POINTER_PATH = config_path.path_join("rom_pointer.smb")
var ROM_PATH = config_path.path_join("baserom.nes")
var ROM_ASSETS_PATH = config_path.path_join("resource_packs/BaseAssets")
const ROM_PACK_NAME := "BaseAssets"
const ROM_ASSETS_VERSION := 8

var server_version := -1
var current_version := -1
var current_snapshot := ""
var version_number := ""
var is_snapshot := false

const LEVEL_THEMES := {
	"SMB1": SMB1_LEVEL_THEMES,
	"SMBLL": SMB1_LEVEL_THEMES,
	"SMBANN": SMB1_LEVEL_THEMES,
	"SMBS": SMBS_LEVEL_THEMES
}

var custom_campaigns := []
var custom_pack := ""
var custom_level_idx := 0
var current_custom_campaign := ""

const SMB1_LEVEL_THEMES := ["Overworld", "Desert", "Snow", "Jungle", "Desert", "Snow", "Jungle", "Overworld", "Space", "Autumn", "Pipeland", "Skyland", "Volcano"]
const SMBS_LEVEL_THEMES := ["Overworld", "Garden", "Beach", "Mountain", "Garden", "Beach", "Mountain", "Overworld", "Autumn", "Pipeland", "Skyland", "Volcano", "Fuck"]

const FORCE_NIGHT_THEMES := ["Space"]
const FORCE_DAY_THEMES := []

signal text_shadow_changed

@onready var player_ghost: PlayerGhost = $PlayerGhost

var debugged_in := true

var score_tween = null
var time_tween = null

var total_deaths := 0

var portable_mode := false
var checked_portable := false

const RESOLUTIONS := [Vector2(256, 240), Vector2(320, 240), Vector2(384, 240), Vector2(426, 240), Vector2(256, 240)]

var score := 0:
	set(value):
		if disco_mode == true:
			if value > score:
				var diff = value - score
				score = score + (diff * 1)
			else:
				score = value
		else:
			score = value
		score = clamp(score, 0, 9999990)
var coins := 0:
	set(value):
		coins = value
		if coins >= 100:#
			if Settings.file.difficulty.inf_lives == 0 and (current_game_mode != GameMode.CHALLENGE and current_campaign != "SMBANN"):
				lives += floor(coins / 100.0)
				AudioManager.play_sfx("1_up", Global.get_game_viewport_camera_2d().get_screen_center_position())
			coins = coins % 100
var time := 300
var inf_time := false
var lives := 3
var world_num := 1:
	set(value):
		world_num = value

var level_num := 1
var disco_mode := false

signal transition_finished
var transitioning_scene := false
var awaiting_transition := false

var current_room_type := Level.RoomType.NORMAL

signal level_complete_begin
signal score_tally_finished

var achievements := "0000000000000000000000000000"

const LSS_GAME_ID := 5

enum AchievementID{
	SMB1_CLEAR, SMBLL_CLEAR, SMBS_CLEAR, SMBANN_CLEAR,
	SMB1_CHALLENGE, SMBLL_CHALLENGE, SMBS_CHALLENGE,
	SMB1_BOO, SMBLL_BOO, SMBS_BOO,
	SMB1_GOLD_BOO, SMBLL_GOLD_BOO, SMBS_GOLD_BOO,
	SMB1_BRONZE, SMBLL_BRONZE, SMBS_BRONZE,
	SMB1_SILVER, SMBLL_SILVER, SMBS_SILVER,
	SMB1_GOLD, SMBLL_GOLD, SMBS_GOLD,
	SMB1_RUN, SMBLL_RUN, SMBS_RUN,
	ANN_PRANK, SMBLL_WORLD9,
	COMPLETIONIST
}

const HIDDEN_ACHIEVEMENTS := [AchievementID.COMPLETIONIST]

var can_time_tick := true:
	set(value):
		can_time_tick = value
		if value == false:
			pass

var player_power_states := "0000"

var connected_players := 1

const CAMPAIGNS := ["SMB1", "SMBLL", "SMBS", "SMBANN"]

var player_characters := [0, 1, 2, 3]:
	set(value):
		player_characters = value
		player_characters_changed.emit()
signal player_characters_changed

signal disco_level_continued

signal frame_rule

var hard_mode := false

var current_campaign := "SMB1"

var death_load := false

var tallying_score := false

var in_title_screen := false

var game_paused := false
var can_pause := true

var fade_transition := false

enum GameMode{NONE, CAMPAIGN, BOO_RACE, CHALLENGE, MARATHON, MARATHON_PRACTICE, LEVEL_EDITOR, CUSTOM_LEVEL, DISCO}

const game_mode_strings := ["Default", "Campaign", "BooRace", "Challenge", "Marathon", "MarathonPractice", "LevelEditor", "CustomLevel", "Disco"]

var current_game_mode: GameMode = GameMode.NONE

var high_score := 0
var game_beaten := false

signal p_switch_toggle
var p_switch_active := false
var p_switch_timer := 0.0
var p_switch_timer_paused := false

var debug_mode := false

var game_viewport

var custom_campaign_jsons := {}

var level_sequence_captured := false

var process_multibind_pressed_buttons: Dictionary[StringName, int] = {}
var physics_multibind_pressed_buttons: Dictionary[StringName, int] = {}

var unpressed_buttons: Dictionary[StringName, bool] = {}

func _ready() -> void:
	if is_snapshot: 
		get_build_time()
		current_snapshot = get_snapshot_version()
	current_version = get_version_number()
	if OS.is_debug_build(): debug_mode = false
	get_server_version()
	setup_config_dirs()
	check_for_rom()
	
	await get_tree().process_frame  # Wait for scene tree to be ready
	game_viewport = get_tree().root.get_node("Wrapper/CenterContainer/SubViewportContainer/SubViewport")
	if game_viewport:
		reparent(game_viewport)

	load_default_translations()
	level_theme_changed.connect(load_default_translations)

func update_theme() -> void:
	theme_override = ""
	time_override = ""
	$ThemeGetter.update_resource()
	ResourceSetterNew.clear_cache()
	level_theme_changed.emit()

func setup_config_dirs() -> void:
	var dirs = [
		"custom_characters",
		"custom_levels",
		"logs",
		"marathon_recordings",
		"resource_packs",
		"saves",
		"screenshots",
		"level_packs",
		"blueprints",
		"mods"
	]

	for d in dirs:
		var full_path = config_path.path_join(d)
		if not DirAccess.dir_exists_absolute(full_path):
			DirAccess.make_dir_recursive_absolute(full_path)
			
	ModsTransfer.move_mods_to_new_path(ModsTransfer.find_mods_in_old_path())

func check_for_rom() -> void:
	rom_path = ""
	rom_assets_exist = false
	if FileAccess.file_exists(ROM_PATH) == false:
		return
	var path = ROM_PATH 
	if FileAccess.file_exists(path):
		if ROMVerifier.is_valid_rom(path):
			rom_path = path
	if DirAccess.dir_exists_absolute(ROM_ASSETS_PATH):
		var pack_json: String = FileAccess.get_file_as_string(ROM_ASSETS_PATH + "/pack_info.json")
		var pack_dict: Dictionary = JSON.parse_string(pack_json)
		if pack_dict.get("version", -1) >= ROM_ASSETS_VERSION:
			rom_assets_exist = true 
		else:
			ResourceGenerator.updating = true
			OS.move_to_trash(ROM_ASSETS_PATH)

func _process(delta: float) -> void:
	if multibind_action_just_pressed("debug_reload"):
		ResourceSetter.cache.clear()
		ResourceSetterNew.clear_cache()
		ResourceGetter.cache.clear()
		AudioManager.current_level_theme = ""
		update_theme()
		TranslationServer.reload_pseudolocalization()
		log_comment("Reloaded resource packs!")
	
	## Imagine being such a shit game engine, that you somehow BROKE ALT-F4, SERIOUSLY.
	if Input.is_key_pressed(KEY_ALT) and Input.is_key_pressed(KEY_4):
		get_tree().quit()
	
	if multibind_action_just_pressed("toggle_fps_count"):
		%FPSCount.visible = !%FPSCount.visible
	if (%FPSCount.visible):
		%FPSCount.text = str(int(Engine.get_frames_per_second())) + " FPS" + get_memory_usage()
	
	handle_p_switch(delta)
	
	handle_input()
	
	# DawnLR: Pluh! It just a quick way to get to the title screen, you can delete it if you want. 👍️👍️👍️
	if OS.is_debug_build():
		if Input.is_key_label_pressed(KEY_F11) and debug_mode == false:
			AudioManager.play_global_sfx("switch")
			debug_mode = true
			log_comment("Debug Mode enabled! some bugs may occur!")
		if Input.is_key_label_pressed(KEY_F10) && debug_mode && get_tree().current_scene is not TitleScreen:
			transition_to_scene("res://Scenes/Levels/TitleScreen.tscn")
	
	# DawnLR: WE ARE ALT+ENTER TO FULLSCREEN!
	if multibind_action_just_pressed("fullscreen_toggle"):
		Settings.toggle_fullscreen()
	if multibind_action_just_pressed("ui_screenshot"):
		take_screenshot()

func get_memory_usage() -> String:
	var string := ""
	
	if (!OS.is_debug_build()):
		return string
		
	var bytes := OS.get_static_memory_peak_usage()
	var kb := bytes / 1024.0
	var mb := kb / 1024.0
	
	string += "\n"
	if (mb >= 1.0):
		string += "%s MB" % str(snappedf(mb, 0.01))
	elif (kb >= 1.0):
		string += "%s KB" % str(snappedf(kb, 0.01))
	else: # If that could ever happen
		string += "%s BYTES" % str(snappedf(bytes, 0.01))
		
	return string + " - MEM USED"

func take_screenshot() -> void:
	var img: Image = Global.get_game_viewport().get_texture().get_image()
	var filename = config_path.path_join("screenshots/screenshot_" + str(int(Time.get_unix_time_from_system())) + ".png")
	var err = img.save_png(filename)
	if !err:
		log_comment("Screenshot Saved!")
	else:
		log_error(error_string(err))

func handle_p_switch(delta: float) -> void:
	if p_switch_active and get_tree().paused == false:
		if p_switch_timer_paused == false:
			p_switch_timer -= delta
		if p_switch_timer <= 0:
			p_switch_active = false
			p_switch_toggle.emit()
			AudioManager.stop_music_override(AudioManager.MUSIC_OVERRIDES.PSWITCH)

func get_build_time() -> String:
	# SkyanUltra: Slightly expanded function to make it easier to get snapshot build numbers.
	var date = Time.get_date_dict_from_system()
	var year_last_two = date.year % 100
	var now = Time.get_unix_time_from_system()
	print("[b][color=cyan]Current unix time:[/color][/b] ", int(now))
	var start_of_year = Time.get_unix_time_from_datetime_dict({
		"year": date.year,
		"month": 1,
		"day": 1,
		"hour": 0,
		"minute": 0,
		"second": 0
	})

	var days_since_year_start = int((now - start_of_year) / 86400)
	@warning_ignore("integer_division")
	var week = int(days_since_year_start / 7) + 1
	var build_date = "%02dw%02d" % [year_last_two, week]
	print_rich("[b][color=cyan]Partial snapshot build ID:[/color][/b] ", build_date)
	return build_date

func get_version_number() -> int:
	var number = (FileAccess.open("res://version.txt", FileAccess.READ).get_as_text())
	version_number = str(number).replace("\n", "")
	return int(number)

func get_snapshot_version() -> String:
	var number = (FileAccess.open("res://snapshot_version.txt", FileAccess.READ).get_as_text())
	number = number.replace("\n", "")
	return number

func get_int_version_num(version_num := "") -> int:
	return int(version_num.replace(".", "").pad_zeros(3))

func player_action_pressed(action := "", player_id = 0) -> bool:
	if SpeedrunHandler.simulating_inputs:
		player_id = "s"
	return Input.is_action_pressed(action + "_" + str(player_id))

func player_action_just_pressed(action := "", player_id = 0) -> bool:
	if SpeedrunHandler.simulating_inputs:
		player_id = "s"
	return multibind_action_just_pressed(action + "_" + str(player_id))

func player_action_just_released(action := "", player_id = 0) -> bool:
	if SpeedrunHandler.simulating_inputs:
		player_id = "s"
	return Input.is_action_just_released(action + "_" + str(player_id))

func tally_time() -> void:
	if tallying_score:
		return
	$ScoreTally.play()
	tallying_score = true
	var target_score = score + (time * 50)
	score_tween = create_tween()
	time_tween = create_tween()
	var duration = float(time) / 120
	duration = min(duration, 5)
	
	score_tween.tween_property(self, "score", target_score, duration)
	time_tween.tween_property(self, "time", 0, duration)
	await score_tween.finished
	tallying_score = false
	$ScoreTally.stop()
	$ScoreTallyEnd.play()
	score_tally_finished.emit()

func cancel_score_tally() -> void:
	if score_tween != null:
		score_tween.kill()
	if time_tween != null:
		time_tween.kill()
	tallying_score = false
	$ScoreTally.stop()

func activate_p_switch() -> void:
	if p_switch_active == false:
		p_switch_toggle.emit()
	AudioManager.set_music_override(AudioManager.MUSIC_OVERRIDES.PSWITCH, 99, false)
	p_switch_timer = 10
	p_switch_active = true

func reset_values() -> void:
	PlayerGhost.idx = 0
	Checkpoint.passed_checkpoints.clear()
	Checkpoint.sublevel_id = 0
	total_deaths = 0
	OnOffSwitcher.active = false
	Door.unlocked_doors = []
	Door.exiting_door_id = -1
	Checkpoint.unlocked_doors = []
	KeyItem.total_collected = 0
	Checkpoint.keys_collected = 0
	Broadcaster.active_channels = []
	Warper.target_channel = -1
	Warper.can_warp = true
	ConditionalClear.valid = true
	ConditionalClear.checked = false
	GlobalCounter.amounts = {}
	Level.start_level_path = Level.get_scene_string(world_num, level_num)
	LevelPersistance.reset_states()
	OffScreenDespawner.editor_testing_safety = false
	Level.first_load = true
	Level.can_set_time = true
	Level.in_vine_level = false
	Level.vine_return_level = ""
	Level.vine_warp_level = ""
	p_switch_active = false
	p_switch_timer = -1.0

func stop_all_timers() -> void:
	p_switch_active = false
	p_switch_timer = -1

func clear_saved_values() -> void:
	coins = 0
	score = 0
	lives = 3
	player_power_states = "0000"

func transition_to_scene(scene_path = "") -> void:
	fade_transition = bool(Settings.file.visuals.transition_animation)
	if transitioning_scene:
		return
	transitioning_scene = true
	if fade_transition:
		freeze_screen()
		$Transition/AnimationPlayer.play("FadeIn")
		await $Transition/AnimationPlayer.animation_finished
		await get_tree().create_timer(0.1, true).timeout
	else:
		%TransitionBlock.modulate.a = 1
		$Transition.show()
		await get_tree().create_timer(0.1, true).timeout
	var wrapper = get_tree().root.get_node("Wrapper")
	wrapper.change_scene_to(scene_path)
	await get_tree().create_timer(0.15, true).timeout
	if fade_transition:
		close_freeze()
		$Transition/AnimationPlayer.play_backwards("FadeIn")
	else:
		$Transition/AnimationPlayer.play("RESET")
		$Transition.hide()
	transitioning_scene = false
	transition_finished.emit()

func do_fake_transition(duration := 0.2) -> void:
	if fade_transition:
		$Transition/AnimationPlayer.play("FadeIn")
		await $Transition/AnimationPlayer.animation_finished
		await get_tree().create_timer(duration, false).timeout
		$Transition/AnimationPlayer.play_backwards("FadeIn")
	else:
		%TransitionBlock.modulate.a = 1
		$Transition.show()
		await get_tree().create_timer(duration + 0.05, false).timeout
		$Transition.hide()

func freeze_screen() -> void:
	if Settings.file.video.visuals == 1:
		return
	$Transition.show()
	$Transition/Freeze.show()
	$Transition/Freeze.texture = ImageTexture.create_from_image(Global.get_game_viewport().get_texture().get_image())

func close_freeze() -> void:
	$Transition/Freeze.hide()

#var recording_dir = "user://marathon_recordings/"
var recording_dir = config_path.path_join("marathon_recordings")

func update_game_status() -> void:
	var lives_str := str(lives)
	if Settings.file.difficulty.inf_lives == 1:
		lives_str = "∞"
	var string := "Coins = " + str(coins) + " Lives = " + lives_str

func open_marathon_results() -> void:
	get_node("GameHUD/MarathonResults").open()

func open_disco_results() -> void:
	get_node("GameHUD/DiscoResults").open()

func on_score_sfx_finished() -> void:
	if tallying_score:
		$ScoreTally.play()

func get_server_version() -> void:
	var http = HTTPRequest.new()
	add_child(http)
	var url = VERSION_CHECK_URL
	if is_snapshot:
		url = SNAPSHOT_CHECK_URL
	http.request_completed.connect(version_got)
	http.request(url, [], HTTPClient.METHOD_GET)

func version_got(_result, response_code, _headers, body) -> void:
	current_version = get_version_num_int(version_number)
	if response_code == 200:
		if is_snapshot:
			server_version = int(get_snapshot_num_int(body.get_string_from_utf8()))
		else:
			server_version = int(get_version_num_int(body.get_string_from_utf8()))
	else:
		server_version = -2

# DawnLR: Just some rewrite, the functionality is still the same

var error_log_cooldown := false
func log_error(msg := "", can_spam := true, timer := 10) -> void:
	msg = tr(msg)
	push_error(msg)
	
	if error_log_cooldown and not can_spam:
		return
	var error_message = %ErrorMessage.duplicate()
	error_message.text = "Error - " + msg
	
	create_log(error_message, timer, can_spam)

func log_warning(msg := "", timer := 10) -> void:
	msg = tr(msg)
	push_warning(msg)
	
	var error_message: Label = %WarningMessage.duplicate()
	error_message.text = "Warning - " + str(msg)
	
	create_log(error_message, timer)

func log_comment(msg := "", timer := 2) -> void:
	msg = tr(msg)
	print(msg)
	
	var error_message = %CommentMessage.duplicate()
	error_message.text = str(msg)
	
	create_log(error_message, timer)

func do_cooldown() -> void:
	error_log_cooldown = true
	await get_tree().create_timer(1, false).timeout
	error_log_cooldown = false

func create_log(error_message: Label, timer: int, can_spam := false) -> void:
	error_message.visible = true
	if can_spam == false:
		do_cooldown()
	$Logs/VBoxContainer.add_child(error_message)
	await get_tree().create_timer(timer, false).timeout
	error_message.queue_free()

func level_editor_is_playtesting() -> bool:
	if level_editor == null:
		return false
	if current_game_mode == GameMode.LEVEL_EDITOR:
		if level_editor.current_state == LevelEditor.EditorState.PLAYTESTING:
			return true
	return false

func level_editor_is_editing() -> bool:
	if level_editor == null:
		return false
	return level_editor.current_state != LevelEditor.EditorState.PLAYTESTING

func unlock_achievement(achievement_id := AchievementID.SMB1_CLEAR) -> void:
	achievements[achievement_id] = "1"
	if achievement_id != AchievementID.COMPLETIONIST:
		check_completionist_achievement()
	SaveManager.write_achievements()

func check_completionist_achievement() -> void:
	if achievements.count("0") == 1:
		unlock_achievement(AchievementID.COMPLETIONIST)

const FONT = preload("res://Assets/Sprites/UI/Font.fnt")

func sanitize_string(string := "") -> String:
	string = string.to_upper()
	for i in string.length():
		if FONT.has_char(string.unicode_at(i)) == false and string[i] != "\n":
			string = string.replace(string[i], " ")
	return string

func get_base_asset_version() -> int:
	var json = JSONParser.parse_to_dict(config_path.path_join("BaseAssets/pack_info.json"))
	var version = json.version
	return get_version_num_int(version)

func get_version_num_int(ver_num := "0.0.0") -> int:
	return int(ver_num.replace(".", ""))

func get_snapshot_num_int(ver_num := "26w00a") -> int:
	var year = ver_num.substr(0, 2)
	var week = ver_num.substr(3, 2)
	var num = ver_num[5]
	
	return (int(year) * int(week)) + int(num.unicode_at(0))

func load_default_translations() -> void:
	for i in lang_codes:
		if i != "gal":
			create_translation_from_json(i)
	create_gal_translation("res://Assets/Locale/en.json")

func create_translation_from_json(locale := "") -> void:
	var locale_json := {}
	for resource_pack in Settings.file.visuals.resource_packs:
		var path = $ResourceSetterNew.get_resource_pack_path("res://Assets/Locale/" + locale + ".json", resource_pack)
		var file_json = JSONParser.parse_to_dict(path)
		for i in file_json.keys():
			var value = file_json[i]
			if value is Dictionary:
				value = $ResourceSetterNew.get_variation_json(value).source
			value = remove_cryllic_characters(value)
			if locale_json.has(i) == false:
				locale_json[i] = value.to_upper()
	var trans = Translation.new()
	trans.locale = locale
	for i in locale_json.keys():
		trans.add_message(i, locale_json[i])
	TranslationServer.remove_translation(TranslationServer.get_translation_object(locale))
	TranslationServer.add_translation(trans)

func remove_cryllic_characters(message := "") -> String:
	const cryllic := "авекмнорстух’"
	const latin := "abekmhopctyx'"
	var idx := 0
	for i in cryllic:
		message = message.replace(i, latin[idx])
		idx += 1
	return message

func create_gal_translation(en_json_path := "") -> void:
	var en_json = JSONParser.parse_to_dict(en_json_path)
	var translation = Translation.new()
	for i in en_json.keys():
		translation.add_message(i, convert_en_to_gal(en_json[i]))
	translation.locale = "gal"
	if TranslationServer.get_translation_object("gal") != null:
		TranslationServer.remove_translation(TranslationServer.get_translation_object("gal"))
	TranslationServer.add_translation(translation)

func convert_en_to_gal(en_string := "") -> String:
	var gal_string = en_string.to_upper()
	var idx := 0
	for i in gal_string:
		if gal_string[idx] in "ABCDEFGHIJKLMNOPQRSTUVWXYZ":
			gal_string[idx] = String.chr(i.unicode_at(0) + 65248)
		idx += 1
	return gal_string

func in_custom_campaign(campaign := current_custom_campaign) -> bool:
	return campaign not in CAMPAIGNS and campaign != ""

func merge_dict(target: Dictionary, source: Dictionary) -> void:
	# SkyanUltra: Used to properly merge dictionaries JSONs rather than out right overwriting entries.
	for key in source.keys():
		if target.has(key) and target[key] is Dictionary and source[key] is Dictionary:
			merge_dict(target[key], source[key])
		else:
			target[key] = source[key]

# i wouldve much preferred to use Wrapper.get_game_viewport() everywhere instead, but i just cannot get Wrapper to be a global without loading twice right now! for the time being, thisll do
func get_game_viewport() -> SubViewport:
	return game_viewport

func get_game_viewport_camera_2d() -> Camera2D:
	return null if game_viewport == null else game_viewport.get_camera_2d()

func nice_json_format(json_string := "") -> String:
	var inside_array := 0
	var inside_obj := 0
	var indents := 0
	var end_reached := false
	for i in json_string.length():
		if json_string[i] == "{":
			inside_obj += 1
			if json_string[i + 1] != "}":
				indents += 1
				json_string = json_string.insert(i + 1, "\n")
				i += 1
				for x in indents:
					json_string = json_string.insert(i + 2, "\t")
					i += 1
		if json_string[i] == "[":
			inside_array += 1
			if inside_array > 1:
				indents += 1
		if json_string[i] == "]":
			inside_array -= 1
			if inside_array > 1:
				indents -= 1
		if json_string[i] == "}":
			if inside_obj > 0:
				indents -= 1
				json_string = json_string.insert(i + 1, "\n")
				i += 1
				for x in indents:
					json_string = json_string.insert(i - 1, "\t")
					i += 1
		if json_string[i] == ",":
			if inside_array <= 0:
				json_string = json_string.insert(i + 1, "\n")
				i += 1
				for x in indents:
					json_string = json_string.insert(i + 2, "\t")
					i += 1
	return json_string

# Like Input.is_action_just_pressed, but it allows pressing
# a button while another bind for it is already pressed.
func multibind_action_just_pressed(action: StringName) -> bool:
	if Engine.is_in_physics_frame():
		return physics_multibind_pressed_buttons.get(action, -1) \
			== Engine.get_physics_frames()
	return process_multibind_pressed_buttons.get(action, -1) \
		== Engine.get_process_frames()

func _input(event: InputEvent) -> void:
	if not event.is_action_type() or not event.is_pressed():
		return
	for action in InputMap.get_actions():
		if event.is_action_pressed(action):
			if event is InputEventJoypadMotion:
				if unpressed_buttons[action] == false:
					return
			unpressed_buttons[action] = false
			process_multibind_pressed_buttons[action] = Engine.get_process_frames()
			# Add 1 physics frame, like Godot also does,
			# because "input may come in part way through a physics tick"
			# https://github.com/godotengine/godot/blob/2327a823578a30f09068f97272598521896d5633/core/input/input.cpp#L1025
			physics_multibind_pressed_buttons[action] = Engine.get_physics_frames() + 1

func handle_input() -> void:
	for action in InputMap.get_actions():
		if Input.is_action_pressed(action) == false:
			unpressed_buttons[action] = true

func warper_cooldown() -> void:
	await get_tree().create_timer(1, false).timeout
	Warper.can_warp = true
